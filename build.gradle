buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = modVersion
group = "moe.nightfall.vic.chat"
archivesBaseName = "vChat"

configurations {
    packed {
        description = 'lib packed during build'
    }
    compile {
        description = 'compile classpath'
        transitive = true
        extendsFrom packed
    }
}

if (System.env.BUILD_NUMBER != null) version = "$version.$System.env.BUILD_NUMBER"

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
	if (file('../run').exists()){
		runDir = "../run"
	} else {
		runDir = "run"
	}    
	replaceIn "Constants.java"
    replace "\${version}", "${modVersion}"
    replace "\${api_version}", "${modAPIVersion}"
    
    mappings = "stable_12"
}

dependencies {
    packed group: "org.twitter4j", name: "twitter4j-core", version: "4.0.4"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
}

jar {
    from { configurations.packed.collect { it.isDirectory() ? it : zipTree(it) } }
    include '**'
}

task apiJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.output
    include 'moe/nightfall/vic/chat/api/**'
    classifier = 'api'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from { configurations.packed.collect { it.isDirectory() ? it : zipTree(it) } }
    include '**'
    classifier = 'deobf'
}

artifacts {
    archives sourcesJar
    archives apiJar
    archives deobfJar
}
